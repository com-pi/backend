plugins {
	id 'java'
	id 'org.springframework.boot' version '3.2.5'
}

subprojects {
	group = 'com.comp'

	sourceCompatibility = JavaVersion.VERSION_17
	targetCompatibility = JavaVersion.VERSION_17

	apply plugin: 'java'
	apply plugin: 'java-library'
	apply plugin: 'org.springframework.boot'
	apply plugin: 'io.spring.dependency-management'

	ext {
		set('springCloudVersion', "2023.0.1")
	}

	dependencyManagement {
		imports {
			mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		}
	}

	configurations {
		compileOnly {
			extendsFrom annotationProcessor
		}
	}

	repositories {
		mavenCentral()
	}

	test {
		useJUnitPlatform()
	}

	wrapper {
		gradleVersion = '8.7'
	}
}

project(':api-gateway') {
	version = 1.0
	dependencies {
		implementation project(':common')
		implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
		testImplementation 'io.projectreactor:reactor-test'
	}
}

project(':discovery-eureka') {
	version = 1.0

	dependencies {
		implementation project(':common')
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-server'
	}
}

project(':board-service') {
	version = 1.0

	dependencies {
		implementation project(':common')
		implementation 'org.springframework.boot:spring-boot-starter-web'
		implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
		implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
		runtimeOnly 'com.h2database:h2'
	}
}

project(':common') {
	dependencies {
		// lombok
		compileOnly 'org.projectlombok:lombok'
		annotationProcessor 'org.projectlombok:lombok'

		// spring boot 기본 설정
		implementation 'org.springframework.boot:spring-boot-starter'
		testImplementation 'org.springframework.boot:spring-boot-starter-test'
	}
}
